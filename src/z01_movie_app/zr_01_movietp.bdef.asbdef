managed implementation in class ZBP_R_01_MOVIETP unique;
strict ( 2 );

define behavior for ZR_01_MovieTP alias Movie
persistent table zabap_movie_a
lock master
authorization master ( instance )
//etag master <field_name>
{
  create ( authorization : global );
  update;
  delete;
  field ( readonly, numbering : managed ) MovieUuid;
  association _Rating { create; }

  field ( mandatory : create ) Title, Genre, PublishingYear, RuntimeInMin;
  field ( readonly : update ) Title, Genre, PublishingYear, RuntimeInMin;
  field ( readonly ) CreatedAt, CreatedBy, LastChangedAt, LastChangedBy;

  validation ValidateGenre on save { create; }

  action RateMovie parameter ZA_01_Rating; //result [1] $self;



  mapping for zabap_movie_a corresponding

    {
      CreatedAt      = created_at;
      CreatedBy      = created_by;
      ImageUrl       = image_url;
      LastChangedAt  = last_changed_at;
      LastChangedBy  = last_changed_by;
      MovieUuid      = movie_uuid;
      PublishingYear = publishing_year;
      RuntimeInMin   = runtime_in_min;
    }
}

define behavior for ZR_01_Rating alias Rating
persistent table zabap_rating_a
lock dependent by _Movie
authorization dependent by _Movie
//etag master <field_name>
{
  update;
  delete;
  association _Movie;
  field ( readonly, numbering : managed ) RatingUuid;
  field ( readonly ) MovieUuid, RatingDate, UserName;


    determination DetermineRatingDate on save { create; }
    determination DetermineUserName on save {create;}

  mapping for zabap_rating_a corresponding
    {
      MovieUuid  = movie_uuid;
      RatingDate = rating_date;
      RatingUuid = rating_uuid;
      UserName   = user_name;
    }
}